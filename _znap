#compdef znap

[[ -v _ZNAP_ARGS ]] ||
  znap &> /dev/null

local -a actions=()
local k; for k in ${(ko)_ZNAP_ARGS}; do
  actions+=( "$k\:'$_ZNAP_ARGS[$k]'" )
done

local ret=1
local -a context line state state_descr
local -A opt_args
_arguments -A '' ":action:(( $actions ))" '*:: :->next'
ret=$?

[[ $state == next ]] ||
  return ret

case $words[1] in
  clean)
    _arguments : '*:: :->dirs' ;;
  clone)
    _arguments : ':remote repository:_urls' ;;
  close-fd)
    _arguments : ':file descriptor:_file_descriptors' ;;
  compile)
    _arguments : '*:: :->files-functions' ;;
  eval)
    _arguments : ': :->repos-caches' ':command:_default' ;;
  function)
    _arguments : ':command:_default' '*::function:_functions' ;;
  help)
    _arguments : ":action:(( $actions ))" ;;
  multi)
    _arguments : ':command:_default' ;;
  prompt)
    _arguments : ': :->repos-themes' '*:: :->subthemes' ;;
  pull)
    _arguments : "*:: :->plugins" ;;
  restart)
    _arguments : ':file:_files' ;;
  rm)
    _arguments : '*:: :->repos-caches' ;;
  source)
    _arguments : ": :->plugins" '*:: :->files' ;;
  status)
    _arguments : "*:: :->plugins" ;;
esac
ret=$(( ret && ? ))

local -aU plugins=( ~znap/*(/) ); plugins=( ${plugins[@]:t} )
case $state in
  dirs)
    _alternative 'directories:directory:_files -/'
    ;;
  files)
    local repo=~znap/$1; [[ $1 == */* ]] &&
      repo=~[$1]
    [[ -d $repo ]] &&
      _arguments : "*::file:_files -W $repo"
    ;;
  repos-caches)
    local -aU caches=( ~znap/*.zsh(#q.) )
    _alternative "repositories:plugin:($plugins)" "cache-files:cache:(${caches[@]:t:r})"
    ;;
  files-functions)
    _alternative "files:file:_files" "functions:function:_functions"
    ;;
  plugins)
    _alternative "repositories:plugin:($plugins)"
    ;;
  repos-themes)
    _alternative "repositories:plugin:($plugins)" "themes:prompt theme:($prompt_themes)"
    ;;
  subthemes)
    local repo=~znap/$1; [[ $1 == */* ]] &&
      repo=~[$1]
    if [[ -d $repo ]]; then
      local -aU themes=( $repo/***.zsh-theme(#q.) )
      themes=( ${themes[@]:t:r} )
      _arguments : "::prompt theme:($themes)"
    fi
    ;;
esac

return $(( ret && ? ))
