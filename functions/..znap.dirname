#!/bin/zsh

# Resolve ~[$1]
..znap.dirname.n() {
  emulate -L zsh; setopt extendedglob

  [[ -z $1 ]] &&
      return 1

  private dir=~znap/$1
  if [[ $1 == */* ]]; then
    dir=~znap/${${1##*/}%.git}
    [[ -d $dir ]] ||
        .znap.clone $1
  fi

  [[ -d $dir ]] ||
      return 1

  reply=( $dir )
  return 0
}

# ~Abbreviate $1
..znap.dirname.d() {
  emulate -L zsh; setopt extendedglob

  if [[ $1 == ~znap/* && -d $1/.git ]]; then
    # private name=${${1#~znap/}:h1}  <- Zsh 5.8+
    private name=${${1#~znap/}%%/*}
    reply=( "$name" ${#:-~znap/$name} )
    return 0
  fi
  return 1
}

# Complete ~[$PREFIX$SUFFIX
..znap.dirname.c() {
  private _P__tag=named-directories
  _tags $_P__tag

  local -a expl=()
  _tags &&
      _requested $_P__tag expl 'named directory' &&
          compadd "$expl[@]" -I ']' -Q - ~znap/*/.git(on-/:h:t)
}

..znap.dirname() {
  ..znap.dirname.$1 "$2"
}

..znap.dirname "$@"
