#!/bin/zsh
# make shallow git clones in parallel to plugins dir
# args: ( <user>/<repo> | <url> ) ...
zmodload -Fa zsh/files b:zf_mkdir

.znap.clone() {
  emulate -L zsh; setopt $_znap_opts

  local dir=${XDG_CONFIG_HOME:-$HOME/.config}/zsh/
  [[ -d $dir ]] ||
    zf_mkdir -p $dir
  local file=$dir/znap-repos
  local -aU lines=(); [[ -f $file ]] &&
    lines=( "${(f)"$(< $file)"}" )
  local -aU repos=( $@ ); (( $#repos )) ||
    repos=( $lines[@] )

  if (( $#repos < 1 )); then
    print -u2 "znap clone: no repositories found in $file"
    .znap.help clone
    return 78
  fi

  local server; zstyle -s :znap:clone: default-server server ||
    server='https://github.com/'

  local -i ret=1
  .znap.multi ".znap.clone.task $server "$^repos
  ret=$?

  lines=( ${lines:#(${repos[@]})} )
  lines+=( $repos[@] )
  print -l ${(i)lines} >| $file

  [[ -v _comp_dumpfile && -f $_comp_dumpfile ]] &&
    zf_rm -f $_comp_dumpfile

  return ret
}

.znap.clone "$@"
