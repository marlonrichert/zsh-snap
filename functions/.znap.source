#!/bin/zsh
# source plugin or repo submodules & scripts
# args: <repo> [ <dir> | <file> ... ]

if (( $# < 1 )); then
  print -u2 'znap source: not enough arguments'
  .znap.help source
  return 64
fi

local -aU src=()
local -i ret=0
() {
  emulate -L zsh; setopt $_znap_opts

  local plugin=$1
  if [[ $plugin == */* ]]; then
    plugin=${${plugin}%.git}
    [[ -d ~[$plugin] ]] ||
      .znap.clone $1
  fi

  shift
  local repo=~[$plugin]
  local -aU files=( $repo ); (( $# > 0 )) &&
    files=( $repo/$^@ )
  local -aU tmp=()
  local -a words ext=( '.plugin.zsh' '(|.*).zsh*' '.sh' '' )
  local f; for f in $files; do
      if [[ -d $f ]]; then
        tmp=( $f/(#i){*(${(~j:|:)${=${f:t}//[-_]/ }:#zsh})*,init,*}${^~ext}~*.zwc(-.rY1) )
      elif [[ -r $f ]]; then
        tmp=( $f )
      else
        tmp=( $f${^~ext}~*.zwc(-.rY1) )
      fi
      src+=( $tmp[1] )
  done
} "$@"

local s; for s in $src; do
  . $s || ret=$?
done

return ret
