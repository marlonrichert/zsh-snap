#!/bin/zsh
emulate -L zsh

setopt $_znap_opts
zmodload -Fa zsh/files b:zf_mkdir b:zf_mv b:zf_rm
autoload -Uz is-at-least

private server=$1 url=$2
[[ $url != (*/*/*|*:*|*.git(|/)) ]] &&
    url=$server$url.git

local gitdir=''
..znap.repos-dir

local -P name=${url#*@*:}
private new='' old=$gitdir/${name:t:r} repo=$gitdir/${name:t2:r}

if [[ -d $old ]]; then
  if new=$gitdir/${${(M)$(
      git -C $old remote get-url origin 2> /dev/null
  )%%[^/:]##/[^/:]##}%.git}; then
    if [[ -d $new ]]; then
      zf_rm -rf $old
    else
      zf_mv $old $new
    fi
  fi
fi

[[ -d $repo/.git ]] &&
    return

zf_mkdir -p $repo
if git -C $gitdir clone --depth 1 --recurse-submodules --shallow-submodules $url $repo; then
  [[ -v _comp_dumpfile && -f $_comp_dumpfile ]] &&
      zf_rm -f $_comp_dumpfile
  .znap.ignore ${repo:t2} '*.zwc'  # Add `*.zwc` to repo's local ignore list.
  .znap.compile $repo &> /dev/null &|
  true
fi
