#!/bin/zsh
# install executables & completion functions
# args: <repo> [--no-path] [--no-fpath] ...
emulate -L zsh; setopt $_znap_opts
zmodload -Fa zsh/files b:zf_ln b:zf_mkdir

if (( $# < 1 )); then
  print -u2 'znap install: not enough arguments'
  .znap.help install
  return $(( sysexits[(i)USAGE] + 63 ))
fi

local -A opts
zparseopts -A opts -E -D -- -no-path -no-fpath

.znap.clone $@ ||
    return

local REPLY=
private -aU funcfiles=()
private d='' link='' pat='[[:alnum:]][[:alnum:]_-]#[[:alnum:]]'

local -a repos=()
local gitdir=
..znap.repos install $@

private repo=
for repo in $repos; do
  repo=~[$repo]

  if ! (( $+opts[--no-path] )) ; then
    path=(
        $repo/{,bin/}$~pat(N*:h)
        $path[@]
    )
  fi
  if ! (( $+opts[--no-fpath] )) ; then
      fpath=(
          $repo/{,{[Cc]ompletions,[Ss]rc,zsh}/($~pat/)#}_$~pat(N^/:h)
          $fpath
      )
  fi

  funcfiles=( $repo/{,[Ff]unctions}/($~pat/)#}$~pat(N^/) )
  (( $#funcfiles )) &&
      autoload -Uz $funcfiles
done
