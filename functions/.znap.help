#!/bin/zsh
# print help text for command
# args: [ <subcommand> ]
zmodload -Fa zsh/mapfile p:mapfile
zmodload -Fa zsh/parameter p:functions

.znap.help() {
  emulate -LR zsh; setopt $_znap_opts

  typeset -gHA _ZNAP_ARGS=()
  local f; for f in ${${(%):-%x}:a:h}/.znap.[^.]##; do
    _ZNAP_ARGS[${${f:t}#.znap.}]=${${(f)mapfile[$f]}[2]#'# '}
  done

  local -i fd=1
  if (( $# > 0 )); then
    if [[ -v functions[.znap.$1] ]]; then
      local -a funcfile=( ${(f)mapfile[${${(%):-%x}:a:h}/.znap.$1]} )
      print -lP -- "Usage: %F{10}znap%f $1 ${funcfile[3]#'# args: '}"
      local help="${funcfile[2]#'# '}"
      local first=${${=help}[1]}
      print -l "${(C)first} ${help#$first }."
      return
    fi
    fd=2
    print -u$fd $@
  fi
  print -Pu$fd -- \
'Usage: %F{10}znap%f <command> [ <argument> ... ]

Commands:'
  local MATCH MBEGIN MEND
  print -l -u$fd "${(ko@)_ZNAP_ARGS/(#m)*/  ${(r:9:)MATCH} $_ZNAP_ARGS[$MATCH]}"
  print -Pu$fd -- '
For more info on a command, type `%F{10}znap%f help <command>`.
'
}

.znap.help "$@"
